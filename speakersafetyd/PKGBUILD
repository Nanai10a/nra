# Maintainer: Nanai Jua <me@n7i.dev>

pkgname=speakersafetyd
pkgver=0.1.9
pkgrel=1
pkgdesc='Rust speaker safety daemon for Asahi Linux'
url='https://github.com/AsahiLinux/speakersafetyd'
arch=(aarch64)
license=(MIT)
makedepends=(
  cargo
)
_srcname=${pkgname}-${pkgver}
source=(
  https://github.com/AsahiLinux/${pkgname}/archive/refs/tags/${pkgver}.tar.gz
  align-debug.patch
)
sha512sums=('b237da8d473a89f6cd93c4cc0bef4a5402688c9e6528c54838abeec1d99ec615f69da29483689312da288cd4c4c0186951f60407d8c743d7bdc5a3c1a293fead'
            '6a41e6523fa22b7d9235032170c9f23253b4b5a02661bb6a5f007d5d98e5a1c43755b8886f3d5b072aa8892b7d642c1070d544206274bdd60e4f03834979a57d')

prepare() {
  cd ${_srcname}

  echo "Applying patch..."
  patch -Np1 < ../align-debug.patch
}

build() {
  cd ${_srcname}

  PREFIX=/usr make all
}

package() {
  cd ${_srcname}

  make install                      \
    DESTDIR=${pkgdir}               \
    UNITDIR=/usr/lib/systemd/system \
    UDEVDIR=/usr/lib/udev/rules.d

  install -Dm644 -t ${pkgdir}/usr/share/doc/${pkgname} README.md
  install -Dm644 -t ${pkgdir}/usr/share/doc/${pkgname} docs/speakers.txt
  install -Dm644 -t ${pkgdir}/usr/share/doc/${pkgname} docs/audump.py

  install -Dm644 -t ${pkgdir}/usr/share/licenses/${pkgname} LICENSE
}

# vim:set ts=8 sts=2 sw=2 et:
